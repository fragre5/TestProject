plugins {
    id 'java'
}

group = 'zozulya.test'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

tasks.register('createPreCommitHook') {
    doLast {
        def gitHooksPath = "${projectDir}/.git/hooks/"
        def preCommitScript = '''
            #!/bin/sh
            
            echo "Verifying the validity of the branch name"
            
            BRANCH=$(git rev-parse --abbrev-ref HEAD)
            
            if [[ $BRANCH == "master" ]]; then
                exit 0
            fi
            
            REGEX="(bugfix|feature|hotfix|develop)/[A-Z]+-[0-9]+/[a-zA-Z0-9_-]+$"
            
            if ! [[ $BRANCH =~ $REGEX ]]; then
                echo "Your commit was rejected due to invalid branch name"
                echo "Please rename your branch following this template '(bugfix|feature|hotfix|develop)/task-id/a-Z0-9_-'"
                exit 1
            fi
        '''.stripIndent().trim()
        new File(gitHooksPath, "pre-commit").write(preCommitScript)
        new File(gitHooksPath, "pre-commit").setExecutable(true)
    }
}

tasks.named('build') {
    dependsOn createPreCommitHook
}

test {
    useJUnitPlatform()
}